
plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: "jacoco"


group 'io.macgyver.okrest3'
version = "${OKREST3_VERSION}"


configurations {
	//all*.exclude group: 'org.codehaus.groovy', module: 'groovy'
	all*.exclude group: 'log4j'
	all*.exclude group: 'xml-apis'
	all*.exclude group: 'commons-logging'
}

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {


	compile "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VERSION}"
	compile "com.squareup.okhttp3:okhttp:${OKHTTP_VERSION}"
	compile "org.slf4j:slf4j-api:${SLF4J_VERSION}"
	compile "com.google.guava:guava:${GUAVA_VERSION}"
	
	testCompile "org.assertj:assertj-core:${ASSERTJ_VERSION}"  
	testCompile "junit:junit:${JUNIT_VERSION}"
	testCompile "com.squareup.okhttp3:mockwebserver:${OKHTTP_VERSION}"
	testCompile "ch.qos.logback:logback-classic:${LOGBACK_VERSION}"
	testCompile "org.slf4j:jul-to-slf4j:${SLF4J_VERSION}"

}


license {
    header rootProject.file('LICENSE')
    strictCheck true
}

task wrapper(type: Wrapper) {
	gradleVersion = "$GRADLE_VERSION"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}
	
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
	
	
}

artifacts {
        archives sourcesJar, javadocJar
}
uploadArchives {
    repositories {
        mavenDeployer {
			
            name = 'macgyver-bintray'
           
			
			if (!project.hasProperty('BINRAY_USER')) {
				project.ext['BINTRAY_USER']=System.getenv("BINTRAY_USER")
			}
			if (!project.hasProperty('BINTRAY_KEY')) {
				project.ext['BINTRAY_KEY'] = System.getenv("BINTRAY_KEY")
			}
				
            repository(id: 'macgyver-bintray', url: 'https://api.bintray.com/maven/lendingclub/OSS/okrest'){
                authentication(userName: BINTRAY_USER, password: BINTRAY_KEY)
			}
            pom.project {
				
				name 'okrest'
				description 'REST Wrapper for OkHttp'
				url 'https://github.com/LendingClub/okrest'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
                    }
                }
				scm {
					connection 'scm:git:git@github.com:LendingClub/okrest.git'
					developerConnection 'scm:git:git@github.com:LendingClub/okrest.git'
					url 'git@github.com:LendingClub/okrest.git'
				}
            	developers {
					developer {
						name 'Rob Schoening'
						email 'robschoening@gmail.com'
					}
				}
            }
        }
    }
}


jacocoTestReport {
    group = "reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
    //classDirectories = fileTree(dir: 'build/classes/main', include: 'com/thc/**')
    //sourceDirectories = fileTree(dir: 'scr/java', include: 'com/thc/**')
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}



if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
}
